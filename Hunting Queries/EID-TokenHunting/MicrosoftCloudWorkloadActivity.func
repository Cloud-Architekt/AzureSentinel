// Does not include MicrosoftServicePrincipalSignInLogs because it's mostly not ingested and therefore no sign-in correlation of 1P sign-ins 
// Using CloudAppEvents for unified schema between Azure and M365
// Side Note: OfficeActivity also supports for linkable identifiers and AzureActivity offers also UniqueTokenId for correlation
let MicrosoftCloudWorkloadActivity = (AppId:string, Lookback:timespan=1d, SessionId:string, UniqueTokenId:string, Workload:dynamic) {
    // Get list of first party apps to flag missing sign-in correlation because of 1P apps and mostly missing MicrosoftServicePrincipalSignInLogs
    let FirstPartyApps = externaldata(AppId: string)
        ["https://raw.githubusercontent.com/merill/microsoft-info/main/_info/MicrosoftApps.json"] with(format='multijson')
        | summarize by AppId;
    // Set parameters to variables
    let ActorAccountObjectId = AppId;
    let ActorSessionId = SessionId;
    let ActorUniqueId = UniqueTokenId;
    let FilteredWorkload = Workload;
    // Add another 28h for lookup to the sign-in logs to find sign-in activities from CAE token
    let SigninLookback = iff(isempty(Lookback), 28h, Lookback +28h);
    // Collecting Events from Microsoft Azure App Connector
    let AzureEvents = CloudAppEvents
    | where Timestamp >ago(Lookback) and AccountType == "Application"
    | extend Workload = "Azure"
    | where Workload == FilteredWorkload or FilteredWorkload == ""
    | where AppInstanceId == "0" and Application == "Microsoft Azure"
    | extend AccountId = coalesce((OAuthAppId), replace_string(tostring(parse_json(RawEventData)["UserId"]), "ServicePrincipal_",""))    
    | where AccountId == (ActorAccountObjectId) or ActorAccountObjectId == ""
    | extend TokenClaims = parse_json(RawEventData)["claims"]
    | extend SessionId =  tostring(parse_json(tostring(TokenClaims))["sid"])
    | where SessionId == (ActorSessionId) or ActorSessionId == ""
    | extend UniqueTokenId = tostring(parse_json(tostring(TokenClaims))["uti"])
    | where UniqueTokenId == (ActorUniqueId) or ActorUniqueId == ""
    | extend TokenIssuedAt = tostring(unixtime_seconds_todatetime(toint(parse_json(tostring(TokenClaims))["iat"])))
    | extend AppId =  tostring(parse_json(tostring(TokenClaims))["appid"])
    | extend Audience = tostring(parse_json(tostring(TokenClaims))["aud"])
    | extend ResourceId = case(
        (Audience contains "https://management.core.windows.net" or Audience contains "https://management.azure.com"), "797f4846-ba00-4fd7-ba43-dac1f8f63013",
        ""
    )
    | extend HttpRequest = parse_json(RawEventData)["httpRequest"]
    | extend RestoredOriginalIpAddress = tostring(IPAddress)
    | extend IPAddress = tostring(parse_json(tostring(HttpRequest))["clientIpAddress"])
    | extend AuthType = ""
    | where isnotempty(UniqueTokenId)
    | project Timestamp, AccountId, AccountType, IsAdminOperation, Workload, Application, ApplicationId, ActivityType, ActivityObjects, ObjectType, ObjectId, OAuthAppId, AuthType, AuditSource, ReportId, IsImpersonated, UniqueTokenId, SessionId, TokenIssuedAt, AppId, ResourceId, IPAddress, RestoredOriginalIpAddress, IPTags, UserAgent, UncommonForUser, LastSeenForUser, RawEventData;
    // Collecting Events from Microsoft 365 App Connector
    let M365Events = CloudAppEvents
    | where Timestamp >ago(Lookback) and AccountType == "Application"
    | where AppInstanceId == "0" and Application != "Microsoft Azure"    
    | where tostring(parse_json(RawEventData)["Workload"]) == FilteredWorkload or FilteredWorkload == "" or FilteredWorkload == "M365"
    | extend AccountId = coalesce((OAuthAppId), replace_string(tostring(parse_json(RawEventData)["UserId"]), "ServicePrincipal_",""))        
    | extend AccountId = iff(isempty(AccountId), coalesce(tostring(parse_json(RawEventData)["AppAccessContext"]["UserObjectId"]), tostring(parse_json(RawEventData)["TokenObjectId"])), AccountId)
    | where AccountId == (ActorAccountObjectId) or ActorAccountObjectId == ""
    | extend AppContextSessionId = tostring(parse_json(RawEventData)["AppAccessContext"]["AADSessionId"])    
    | extend SessionId = iff(isnotempty(AppContextSessionId), AppContextSessionId, tostring(parse_json(RawEventData)["SessionId"]))
    | where SessionId == (ActorSessionId) or ActorSessionId == ""        
    | extend UniqueTokenId = tostring(parse_json(RawEventData)["AppAccessContext"]["UniqueTokenId"])
    | where UniqueTokenId == (ActorUniqueId) or ActorUniqueId == ""        
    | extend TokenIssuedAt = tostring(parse_json(RawEventData)["AppAccessContext"]["IssuedAtTime"])
    | extend AppId = tostring(coalesce(parse_json(RawEventData)["AppAccessContext"]["ClientAppId"], parse_json(RawEventData)["ClientAppId"]))
    | extend ResourceId = tostring(parse_json(RawEventData)["AppAccessContext"]["APIId"])
    | extend IPAddress = coalesce(tostring(parse_json(RawEventData)["ClientIP"]), tostring(parse_json(RawEventData)["ClientIPAddress"]))
    // Some ClientIPAddress values contain port, remove port number from IPAddress
    | extend IPAddress = iff(IPAddress matches regex @"^(25[0-5]|2[0-4]\d|1\d\d|[1-9]?\d)(\.(25[0-5]|2[0-4]\d|1\d\d|[1-9]?\d)){3}:\d+$", split(IPAddress, ":")[0], IPAddress)
    // Restored IP Address is only available in OfficeActivity and limited to SharePoint (https://learn.microsoft.com/en-us/entra/global-secure-access/how-to-view-enriched-logs#how-to-view-the-logs)
    | extend RestoredOriginalIpAddress = ""
    | extend Workload = tostring(parse_json(RawEventData)["Workload"])
    | extend AuthType = tostring(parse_json(RawEventData)["AuthenticationType"])
    | project Timestamp, AccountId, AccountType, IsAdminOperation, Workload, Application, ApplicationId, ActivityType, ActivityObjects, ObjectType, ObjectId, OAuthAppId, AuthType, AuditSource, ReportId, IsImpersonated, UniqueTokenId, SessionId, TokenIssuedAt, AppId, ResourceId, IPAddress, RestoredOriginalIpAddress, IPTags, UserAgent, UncommonForUser, LastSeenForUser, RawEventData;
    // Collecting Microsoft Graph API Events from XDR Hunting table (GraphAPIAuditEvents) 
    let MsGraphApiEvents = GraphAPIAuditEvents
    | extend Workload = strcat("Microsoft Graph API", " (", TargetWorkload, ")")
    | where Workload startswith FilteredWorkload or FilteredWorkload == ""
    | where Timestamp >ago(Lookback)
    | extend AccountId = ApplicationId
    | where AccountId == (ActorAccountObjectId) or ActorAccountObjectId == ""
    | where UniqueTokenIdentifier == (ActorUniqueId) or ActorUniqueId == ""  
    // Session and DeviceId is not available in GraphAPIAuditEvents, no support for filtering data on the parameter at this time of the query
    | extend RawEventData = pack_all()
    | extend IsViewPost = RequestMethod == "POST" and (RequestUri has_any("checkAccess", "/get", "/checkMember", "/directory/estimateAccess", "/microsoft.graph.check", "/microsoft.graph.getMember", "/microsoft.graph.getMemberGroups") or RequestUri contains "/get")
    | extend ActivityType = case(
        RequestMethod in ("GET"), "View",
        RequestMethod in ("PATCH", "PUT"), "Edit",
        IsViewPost, "View",
        RequestMethod == "POST" and RequestUri !endswith "$batch", "Edit",
        RequestMethod == "DELETE", "Delete",
        "Basic"
    )
    // Parsing Uri for Enrichment (by Fabian Bader: https://cloudbrothers.info/en/detect-threats-microsoft-graph-logs-part-1/)
    | extend NormalizedRequestUri = replace_regex(RequestUri, @'[0-9a-fA-F]{8}\b-[0-9a-fA-F]{4}\b-[0-9a-fA-F]{4}\b-[0-9a-fA-F]{4}\b-[0-9a-fA-F]{12}', @'<UUID>')
    | extend NormalizedRequestUri = replace_regex(NormalizedRequestUri, @'\d+$', @'<UUID>')
    | extend NormalizedRequestUri = replace_regex(NormalizedRequestUri, @'\/+', @'/')
    | extend NormalizedRequestUri = replace_regex(NormalizedRequestUri, @'\/(v1\.0|beta)\/', @'/version/')
    | extend NormalizedRequestUri = replace_regex(NormalizedRequestUri, @'%23EXT%23', @'')
    | extend NormalizedRequestUri = replace_regex(NormalizedRequestUri, @'\/[a-zA-Z0-9+_.\-]+@[a-zA-Z0-9.]+\/', @'/<UUID>/')
    | extend NormalizedRequestUri = replace_regex(NormalizedRequestUri, @'^\/<UUID>', @'')
    | extend NormalizedRequestUri = replace_regex(NormalizedRequestUri, @'\?.*$', @'')
    | extend NormalizedRequestUri = tostring(NormalizedRequestUri)
    | extend ObjectType = case(
        split(NormalizedRequestUri, "/")[2] == "<UUID>", split(NormalizedRequestUri, "/")[3],
        split(NormalizedRequestUri, "/")[2] == "me" and split(NormalizedRequestUri, "/")[4] == "$value", split(NormalizedRequestUri, "/")[3],
        split(NormalizedRequestUri, "/")[2] == "me", split(NormalizedRequestUri, "/")[4],
        split(NormalizedRequestUri, "/")[3] == "<UUID>", split(NormalizedRequestUri, "/")[4],
        split(NormalizedRequestUri, "/")[3] == "me", split(NormalizedRequestUri, "/")[4],
        split(NormalizedRequestUri, "/")[3]
    )
    | extend ObjectType = tostring(ObjectType)
    | extend ObjectId = case(
        split(NormalizedRequestUri, "/")[3] == "drives", split(NormalizedRequestUri, "/")[4],
        NormalizedRequestUri contains "/me/", AccountId,
        extract(@'([0-9a-fA-F]{8}\b-[0-9a-fA-F]{4}\b-[0-9a-fA-F]{4}\b-[0-9a-fA-F]{4}\b-[0-9a-fA-F]{12})', 1, RequestUri)
    )
    | extend OAuthAppId = tostring(ApplicationId)
    | extend AuditSource = "GraphAPIAuditEvents"
    // GraphAPIAuditEvents includes restored IP address
    | extend IPAddress = IpAddress
    | extend RestoredOriginalIpAddress = IpAddress
    | extend UniqueTokenId = UniqueTokenIdentifier
    | extend ActivityObjects = ""
    // Following columns and data are not available in GraphAPIAuditEvents
    | extend UserAgent = ""
    | extend Application = ""
    // Graph API is a XDR hunting table and not ingested by App Connector, therefore no ApplicationId from MDA available
    | extend ApplicationId = toint("")
    | extend OAuthAppId = ApplicationId
    | extend AppId = ApplicationId
    | extend ResourceId = ""
    // Following CloudAppEvents-specific columns are not available
    | extend AuthType = "OAuth"
    | extend AccountType = ""
    | extend IsImpersonated = tobool("")
    | extend IsAdminOperation = tobool("")
    | extend TokenIssuedAt = ""
    | extend IPTags = todynamic("")
    | extend UncommonForUser = todynamic("")
    | extend LastSeenForUser = todynamic("")
    | extend Type = "Resource"
    | extend Role = "Target object"
    | extend Name = ObjectId
    | extend Id = RequestUri
    // Summarize some event details to provide ActivityObjects column (similar to CloudAppEvents)
    | extend ActivityObjects = bag_pack_columns(Type, Role, Name, Id)
    | project Timestamp, AccountId, AccountType, IsAdminOperation, Workload, Application, ApplicationId, ActivityType, ActivityObjects, ObjectType, ObjectId, OAuthAppId, AuthType, AuditSource, ReportId, IsImpersonated, UniqueTokenId, SessionId, TokenIssuedAt, AppId, ResourceId, IPAddress, RestoredOriginalIpAddress, IPTags, UserAgent, UncommonForUser, LastSeenForUser, RawEventData;
    union AzureEvents, M365Events, MsGraphApiEvents
    // Correlation with sign-in events in Microsoft Entra
    | join kind=leftouter (
        union AADManagedIdentitySignInLogs, AADServicePrincipalSignInLogs, SigninLogs, AADNonInteractiveUserSignInLogs
            | where CreatedDateTime >ago(SigninLookback) and ResultType == "0"
            | extend AccessType = iff(Category == "SignInLogs" or Category == "NonInteractiveUserSignInLogs", "Delegated", "Application")
            | extend AuthProcessDetails = replace_string(AuthenticationProcessingDetails, " ", "")
            | extend AuthProcessDetails = replace_string(AuthProcessDetails, "\r\n", "")
            | parse-where AuthProcessDetails with * "IsCAEToken\",\"value\":\"" IsTokenCAE"\"" *
            | extend PrincipalId = iff(isnotempty(UserId), UserId, ServicePrincipalId)
            | extend PrincipalType = case(
                Type == "AADServicePrincipalSignInLogs", "ServicePrincipal",
                Type == "AADManagedIdentitySignInLogs", "ManagedIdentity",
                Type == "SigninLogs" or Type == "AADNonInteractiveUserSignInLogs", "User",
                "Unknown")            
            | extend Credential = case(
                isnotempty(ServicePrincipalCredentialKeyId) and ServicePrincipalCredentialKeyId != "00000000-0000-0000-0000-000000000000", bag_pack("CredentialKeyId", ServicePrincipalCredentialKeyId),
                isnotempty(FederatedCredentialId), bag_pack("FederatedCredentialId", FederatedCredentialId),
                isnotempty(ServicePrincipalCredentialThumbprint), bag_pack("CredentialThumbprintId", ServicePrincipalCredentialThumbprint),
                "N/A"
                )
            | project
                SignInTime = CreatedDateTime,
                ClientCredentialType,
                Credential,
                ManagedServiceIdentity,
                UserAgent,
                SigninIpAddress = IPAddress,
                SigninSessionId = SessionId,
                UniqueTokenId = UniqueTokenIdentifier,
                IsTokenCAE,
                AccessType,
                PrincipalId,
                PrincipalType
    ) on UniqueTokenId
    // SessionId is not available in GraphAPIAuditEvents and partly not available for some events in CloudAppEvents, fallback to use SessionId from SigninLogs
    | extend SessionId = coalesce(SessionId, SigninSessionId)
    | extend AccessType = case(
        isnotempty(AccessType), AccessType,
        AccountId in~ (FirstPartyApps), "MicrosoftFirstPartyApp",
        "Unknown"
    )
    | where SessionId == (ActorSessionId) or ActorSessionId == ""
    | extend CorrelatedSignInEvent = iff(isnotempty(SignInTime), true, false)
    | project Timestamp, AccountId, AccessType, PrincipalId, PrincipalType, CorrelatedSignInEvent, SignInTime, SessionId, UniqueTokenId, Workload, ReportId, ActivityType, ActivityObjects, ObjectType, ObjectId, IPAddress, IPTags, RawEventData, LastSeenForUser, UncommonForUser, Credential, ClientCredentialType, IsTokenCAE
    | sort by Timestamp
};
MicrosoftCloudWorkloadActivity(
    AppId="",
    Lookback=1h,
    SessionId="",
    UniqueTokenId="",
    Workload="")
