let UebaEnrichedSignIns = (AccountUpn:string, Lookback:timespan=1d, UserSessionId:string, UniqueTokenId:string) {
    BehaviorAnalytics
    | where TimeGenerated >ago(Lookback) and UserPrincipalName contains (AccountUpn)
    | where EventSource == "Azure AD"
    | extend IPAddress = SourceIPAddress
    | where isnotempty(UserPrincipalName) and isnotempty(ActivityInsights.App) and isnotempty(ActivityInsights.Resource)
    | extend AppDisplayName = tostring(ActivityInsights.App), ResourceDisplayName = tostring(ActivityInsights.Resource), DeviceName = tolower(SourceDevice)
    | join kind=inner (
        union AADNonInteractiveUserSignInLogs, SigninLogs
        | where SessionId contains (UserSessionId) and UniqueTokenIdentifier contains (UniqueTokenId)
        | where CreatedDateTime >ago(Lookback) and UserPrincipalName contains (AccountUpn)
        | extend DeviceDetail = iff(isempty( DeviceDetail_dynamic ), todynamic(DeviceDetail_string), DeviceDetail_dynamic)
        | extend DeviceName = tostring(tolower(DeviceDetail.displayName))
    ) on UserPrincipalName, IPAddress, AppDisplayName, ResourceDisplayName, DeviceName
    | project-rename SignInTime = CreatedDateTime, BehaviorActivityTime = TimeGenerated
    | where (BehaviorActivityTime - SignInTime) between (0min .. 1h)
    | project-reorder SessionId, BehaviorActivityTime, SignInTime
    | extend SignInDetail = bag_pack_columns(SignInTime, AppDisplayName, ResourceDisplayName, RiskLevel, RiskLevelDuringSignIn, SessionId, OriginalRequestId, UniqueTokenIdentifier)
    | summarize SignInDetails =  array_sort_asc(make_set(SignInDetail)), UsersInsights = make_set(UsersInsights), DevicesInsights = make_set(DevicesInsights), ActivityInsights = make_set(ActivityInsights) by BehaviorActivityTime, ActivityType, ActionType, InvestigationPriority, UserPrincipalName, SourceIPAddress
};
UebaEnrichedSignIns(AccountUpn="", UserSessionId="", UniqueTokenId="")
